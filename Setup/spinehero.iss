; Script generated by the Inno Script Studio Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#include <idp.iss>
#define ApplicationVersion GetFileVersion('..\Spine Hero\bin\Release\SpineHero.exe')


[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{AAAAAAAA-AAAA-AAAA-AAAA-AAAAAAAAAAAA}}
AppName=Spine Hero
AppVersion={#ApplicationVersion}
AppContact=info@spinehero.com
AppPublisher=Spine Hero
AppPublisherURL=http://spinehero.com
AppSupportURL=http://spinehero.com
AppUpdatesURL=http://spinehero.com

ArchitecturesAllowed=x64
ArchitecturesInstallIn64BitMode=x64
; MinVersion is Windows 7
MinVersion=6.1.7600

OutputBaseFilename=SetupSpineHero-{#ApplicationVersion}
SolidCompression=yes
PrivilegesRequired=poweruser
DefaultDirName={pf}\SpineHero
DefaultGroupName=Spine Hero
AllowNoIcons=yes
SetupIconFile=..\Spine Hero\Spine Hero.ico
UninstallDisplayName=Spine Hero
UninstallDisplayIcon=..\Spine Hero\Spine Hero.ico

; To setup SignTool:
; Tools > Configure Sign Tool > Add
; Name: mycustom
; Command: "c:\Program Files (x86)\Windows Kits\10\bin\10.0.16299.0\x64\signtool.exe" $p
SignTool=mycustom sign /f {#SourcePath}SH-codecert.pfx /t http://timestamp.comodoca.com/authenticode /p SpineHero16 $f
SignTool=mycustom sign /f {#SourcePath}SH-codecert.pfx /as /fd sha256 /tr http://timestamp.comodoca.com/rfc3161 /td sha256 /p SpineHero16 $f

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
; Name: "czech"; MessagesFile: "compiler:Languages\Czech.isl"
Name: "slovak"; MessagesFile: "Languages\Slovak.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "..\Spine Hero\bin\Release\*"; DestDir: "{app}"; Flags: recursesubdirs ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\Spine Hero"; Filename: "{app}\SpineHero.exe"
Name: "{commondesktop}\Spine Hero"; Filename: "{app}\SpineHero.exe"; Tasks: desktopicon

[Run]
Filename: "{app}\SpineHero.exe"; Flags: nowait postinstall skipifsilent runascurrentuser; Description: "{cm:LaunchProgram,Spine Hero}"

[InstallDelete]
Type: files; Name: "{app}\Squirrel.*"

[PreCompile]
Name: "Pre-Compile-Build.bat"; Flags: abortonerror cmdprompt

[Code]
function Framework462IsNotInstalled(): Boolean;
var
  bSuccess: Boolean;
  regVersion: Cardinal;
begin
  Result := True;

  bSuccess := RegQueryDWordValue(HKLM, 'Software\Microsoft\NET Framework Setup\NDP\v4\Full', 'Release', regVersion);
  if (True = bSuccess) and (regVersion >= 394806) then begin
    Result := False;
  end;
end; 


procedure InitializeWizard;
begin
  if Framework462IsNotInstalled() then
  begin
    idpAddFile('http://go.microsoft.com/fwlink/?LinkId=528222', ExpandConstant('{tmp}\NetFrameworkInstaller.exe'));
    idpDownloadAfter(wpReady);
  end;
end;

procedure InstallFramework;
var
  StatusText: string;
  ResultCode: Integer;
begin
  StatusText := WizardForm.StatusLabel.Caption;
  WizardForm.StatusLabel.Caption := 'Installing .NET Framework 4.6.2 This might take a few minutes…';
  WizardForm.ProgressGauge.Style := npbstMarquee;
  try
    if not Exec(ExpandConstant('{tmp}\NetFrameworkInstaller.exe'), '/passive /norestart', '', SW_SHOW, ewWaitUntilTerminated, ResultCode) then
    begin
      MsgBox('.NET installation failed with code: ' + IntToStr(ResultCode) + '.', mbError, MB_OK);
    end;
  finally
    WizardForm.StatusLabel.Caption := StatusText;
    WizardForm.ProgressGauge.Style := npbstNormal;

    DeleteFile(ExpandConstant('{tmp}\NetFrameworkInstaller.exe'));
  end;
end;

procedure CurStepChanged(CurStep: TSetupStep);
begin
  case CurStep of
    ssPostInstall:
      begin
        if Framework462IsNotInstalled() then
        begin
          InstallFramework();
        end;
      end;
  end;
end;


procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
begin
  case CurUninstallStep of
    usPostUninstall:
      begin
        // Remove garbage (autostart) from registry
        RegDeleteValue(HKEY_CURRENT_USER, 'Software\Microsoft\Windows\CurrentVersion\Run', 'Spine Hero');
      end;
  end;
end;
